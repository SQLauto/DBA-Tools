IF EXISTS(SELECT 1 FROM sys.procedures WHERE schema_id = SCHEMA_ID('dbo') AND name = 'XE_DeleteOldLogFiles')
      DROP PROCEDURE [dbo].[XE_DeleteOldLogFiles]
GO

SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO

/*
Title : This proc will cleanup Log Files Generated by the Xevent.


Description:

Once imported via the XEvents job, Log files are just move to the Archive folder so that they can be reimported in case of error
This proc will delete files in Archived Files older than 1 Day

Il will also delete files in Import folder older than 10 days, if they still haven't been imported

EXEC [XE_DeleteOldLogFiles] @Debug = 1
History:
	2017-04-06 - XMO - Creation
*/
CREATE PROCEDURE [dbo].[XE_DeleteOldLogFiles]
(
	@Debug BIT = 0
)
AS
BEGIN TRY

	DECLARE @Path VARCHAR(256) = CAST(DBA.Indus.GetTokenValue('Path_XEvents') AS VARCHAR(255))+'\'
	--Create Powershell Command to delete XEL files older than 10 days in root folder
	DECLARE @Cmd_delete_old_files NVARCHAR(1000) = 'Get-ChildItem -Path ' + @Path +' | Where-Object {($_.Extension -eq ".xel") -and ($_.LastWriteTime -lt (Get-Date).AddDays(-10)) } | Remove-Item ;'
	--Also Delete Archive Folder older than 1 day
	SET @Cmd_delete_old_files += 'Get-ChildItem -Path ' + @Path +'Archives\ | Where-Object {($_.Extension -eq ".xel") -and ($_.LastWriteTime -lt (Get-Date).AddDays(-1)) } | Remove-Item '

	--Since our powershell command has pipes, it won't work with CMD as is, and must be sent encoded
	DECLARE @source VARBINARY(MAX) = CONVERT(VARBINARY(MAX), @Cmd_delete_old_files)
	DECLARE @encoded VARCHAR(MAX) = CAST('' AS XML).value('xs:base64Binary(sql:variable("@source"))', 'varchar(max)')
	
	If @Debug = 1
		SELECT @Cmd_delete_old_files AS DEBUG_PreEncoded_Cmd

	SET @Cmd_delete_old_files  = 'powershell -EncodedCommand '+@encoded+''

	If @Debug = 0 
		EXEC master.dbo.xp_cmdshell @Cmd_delete_old_files

END TRY
BEGIN CATCH
	IF XACT_STATE() <> 0 ROLLBACK TRANSACTION;
	THROW;
END CATCH
GO

GRANT EXECUTE ON [dbo].[XE_DeleteOldLogFiles] TO [public];
GO

